import vm
import openstack
import ssh

#define the cluster object
#in this case, we make it responsible for connecting to OpenStack
entity MyCluster:
    string connection_url
    string username
    string password
    string tenant
    string network_name
    string image_id 
    string flavor
end

#connect the key
ssh::Key key [1] -- [0:] MyCluster clusters

#connect the OS
std::OS os [1] -- [0:] MyCluster clusters

#objects needed by hosts, expose via relation
vm::IaaS iaas [1] -- [0:] MyCluster clusters
vm::Network network [1] -- [0:] MyCluster clusters

implementation connection for MyCluster:
    provider = openstack::OpenstackConfig(
        connection_url=connection_url, 
        username=username,
        password=password,
        tenant=tenant)  
    self.iaas = vm::IaaS(name = "iaas_openstack", route_script = "", config = provider)
    self.network=vm::Network(name=network_name)
end

implement MyCluster using connection


entity MyHost extends vm::Host:
end

MyCluster cluster [1] -- [0:] MyHost hosts

implementation myhost for MyHost:
    env_name = std::environment_name()
    env_id = std::environment()
    env_server = std::environment_server()
    port = 8888

    self.image = cluster.image_id
    self.network =  cluster.network
    self.user_data = std::template("mymodule/user_data.tmpl")
    self.public_key = cluster.key
    self.os = cluster.os
    self.flavor = cluster.flavor
    self.iaas = cluster.iaas
end

# use our implemenation
# and also the catchall std::hostDefaults
# and vm::ipFact which will discover the ip for this host using the OpenStack API's
implement MyHost using myhost, std::hostDefaults, vm::ipFact
