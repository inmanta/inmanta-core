"""
    Inmanta LSM
    :copyright: 2024 Inmanta
    :contact: code@inmanta.com
    :license: Inmanta EULA
"""


import lsm
import lsm::fsm

entity ServiceWithOrderedAllocation extends lsm::ServiceEntity:
    """
    This service entity demonstrates how to enforce a specific order during
    the allocation process. Here we want to allocate the attributes in the
    following order:
        - value_allocated_first
        - value_allocated_last
    """

    string                      name
    lsm::attribute_modifier     name__modifier="rw"
    string?                     value_allocated_first
    lsm::attribute_modifier     value_allocated_first__modifier="r"
    string?                     value_allocated_last
    lsm::attribute_modifier     value_allocated_last__modifier="r"
end


implementation allocate_in_order for ServiceWithOrderedAllocation:
    # Regular allocation:
    self.value_allocated_first = ordered_allocation(
        self,
        "value_allocated_first"
    )

    # Passing value_allocated_first as a parameter to this allocator
    # will enforce the ordering:
    self.value_allocated_last = ordered_allocation(
        self,
        "value_allocated_last",
        requires=[self.value_allocated_first]
    )
end

implement ServiceWithOrderedAllocation using parents, allocate_in_order


ordered_allocation_binding = lsm::ServiceEntityBindingV2(
    service_entity="allocatorv3_demo::ServiceWithOrderedAllocation",
    lifecycle=lsm::fsm::simple,
    service_entity_name="allocation_order_enforcement",
)

for assignment in lsm::all(ordered_allocation_binding):
    ServiceWithOrderedAllocation(
        instance_id=assignment["id"],
        entity_binding=ordered_allocation_binding,
        name=assignment["attributes"]["name"],
    )
end
