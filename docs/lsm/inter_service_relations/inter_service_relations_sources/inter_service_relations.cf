import lsm
import lsm::fsm

entity Parent extends lsm::ServiceEntity:
    """
        Definition Parent

        :attr name: The name of the parent
    """
    string name
end

index Parent(instance_id)

Child.parent_entity [1] lsm::__service__, lsm::__rwplus__ Parent

entity Child extends lsm::ServiceEntity:
    """
        Definition Child

        :attr name: The name of the child
    """
    string name
end

index Child(instance_id)

implement Parent using parents
implement Child using parents

binding_parent = lsm::ServiceEntityBinding(
service_entity="__config__::Parent",
lifecycle=lsm::fsm::service_with_delete_validate,
service_entity_name="parent_service",
)

binding_child = lsm::ServiceEntityBinding(
service_entity="__config__::Child",
lifecycle=lsm::fsm::service_with_delete_validate,
service_entity_name="child_service",
)


for instance in lsm::all(binding_parent):
    Parent(
        instance_id = instance["id"],
        entity_binding = binding_parent,
        **instance["attributes"],
    )
end

for instance in lsm::all(binding_child):
    Child(
        instance_id = instance["id"],
        entity_binding = binding_child,
        name = instance["attributes"]["name"],
        parent_entity = Parent[instance_id=instance["attributes"]["parent_entity"]]
    )
end
